\section{Erlang Processes}

\subsection{Reduced Latency}
The secret of reducing latency is simple. We try to deliver rendered HTML
as soon as possible and render JavaScript only after WebSocket initialization is complete.
It takes three steps and three Erlang processes to achieve that.

\includeimage{images/page-lifetime.png}{Page Lifetime}

\paragraph{}
Starting the page lifetime N2O uses HTTP process to serve the first HTML page.
After that it dies and spawns Transition process.
Then the browser initiates WebSocket connections to the similar URL endpoint.
N2O creates persistent WebSocket process and the Transition process dies.

\paragraph{}
Your page could also spawn processes with {\bf wf:async}.
These are the persistent processes acting like regullar Erlang processes.
It is a usual approach for organizing non-blocking UI for file uploads and time consuming operations.

\newpage
\subsection{Page Process}
The first HTTP handler only renders HTML. All created
JavaScript actions are stored in the transition process.

\vspace{1\baselineskip}
\begin{lstlisting}
    transition(Actions) ->
        receive {'N2O',Pid} -> Pid ! Actions end.
\end{lstlisting}

\paragraph{}
HTTP handler dies immediately after returning HTML. Transition process
waits for a request from WebSocket handler.

\subsection{Transition Process}
Right after receiving HTML the browser initiates WebSocket connection
thus starting WebSocket handler on the server. After returning
JavaScript actions the Transition process dies and the only process left
running is WebSocket handler. At this point initialization phase is complete.

\subsection{Events Process}
After that all client/server communication is performed over
WebSocket channel. All events coming from the browser are
handled by N2O, which renders elements to HTML and actions to
JavaScript. Each user at any time has only one WebSocket process
per connection.

\subsection{Async Processes}
These are the user processes created with {\bf wf:async}.
That's a legacy name coming from the times when async technology
was called COMET for XHR channel. Async processes are optional
and are only neededed when you have a UI event taking too much
time to be processed, like gigabyte file uploads. You can create
many Async Processes per user.

\subsection{SPA Mode}
In SPA mode you N2O can serve no HTML at all. N2O elements are being
binded during INIT handshake and thus can be used regullary as in DSL mode.

\includeimage{images/static-serving.png}{Static Serving}

\paragraph{}
\section{Endpoints}
N2O Erlang Processes are instantiated and run by Web Server.
Depending on the Web Server endpoint bindings you can specify
module for HTTP requests handling.

\paragraph{}
N2O comes with three endpoint handlers for each Web Server supported.
However you are not limited to use any of these.
You can implement your own endpoint handlers, e.g. for using with
meteor.js or angular.js and providing Erlang back-end event streaming
from server-side. Here is an example of using HTTP, WebSocket and
REST endpoint handlers with Cowboy Web Server.

\vspace{1\baselineskip}
\begin{lstlisting}
    {"/rest/:resource", rest_cowboy, []},
    {"/rest/:resource/:id", rest_cowboy, []},
    {"/ws/[...]", bullet_handler, [{handler, n2o_bullet}]},
    {'_', n2o_cowboy, []}
\end{lstlisting}

\subsection{HTML Pages over HTTP}
This handler is used for serving initial dynamic HTML page.
In case you are serving static HTML content this handler is
not included into the running stack. {\bf {n2o}\_{cowboy}} is
a default HTML page handler.

\paragraph{}
On initial page load {\bf {wf}\_{core}:run} is started during
which {\bf {wf}\_{core}:render} proceeds by calling {\bf Module:main} which
is selected by the routing handler.

\subsection{JavaScript Events over WebSocket}
JavaScript handler shares the same router information as the
HTML handler because during its initial phase the same chain
of N2O handlers is called.

\paragraph{}
This handler knows how to deal with XHR and WebSocket requests.
{\bf {n2o}\_{bullet}} is a default JavaScript event handler
based on Bullet library created by LoÃ¯c Hoguin.

\newpage
\subsection{HTTP API over REST}
REST handler's request context initizlization differs for the one
used by HTML and JavaScript handlers. N2O handler chains are not
applied to REST requests. {\bf rest\_cowboy} is a default REST
handler.

\vspace{1\baselineskip}
\begin{lstlisting}
    {"/rest/:resource", rest_cowboy, []},
    {"/rest/:resource/:id", rest_cowboy, []},
\end{lstlisting}

\lstset{captionpos=b}
\vspace{1\baselineskip}
\begin{lstlisting}[caption=users.erl]
    -module(users).
    -behaviour(rest).
    -compile({parse_transform, rest}).
    -include("users.hrl").
    -export([init/0, populate/1, exists/1, get/0, get/1, post/1, delete/1]).
    -rest_record(user).

    init() -> ets:new(users, [public, named_table, {keypos, #user.id}]).
    populate(Users) -> ets:insert(users, Users).
    exists(Id) -> ets:member(users, wf:to_list(Id)).
    get() -> ets:tab2list(users).
    get(Id) -> [User] = ets:lookup(users, wf:to_list(Id)), User.
    delete(Id) -> ets:delete(users, wf:to_list(Id)).
    post(#user{} = User) -> ets:insert(users, User);
    post(Data) -> post(from_json(Data, #user{})).
\end{lstlisting}
\vspace{1\baselineskip}

To add users to in-memory storage perform POST requests:

\vspace{1\baselineskip}
\begin{lstlisting}
    curl -i -X POST -d "id=vlad" localhost:8000/rest/users
    curl -i -X POST -d "id=doxtop" localhost:8000/rest/users
    curl -i -X GET localhost:8000/rest/users
    curl -i -X PUT -d "id=5HT" localhost:8000/rest/users/vlad
    curl -i -X GET localhost:8000/rest/users/5HT
    curl -i -X DELETE localhost:8000/rest/users/5HT
\end{lstlisting}
\vspace{1\baselineskip}

\section{Handlers}
HTML and JavaScript Web Server HTTP handlers share the same system
of context initialization. 

\vspace{1\baselineskip}
\begin{lstlisting}
    init_context(Req) -> #context{
        actions=[], module=index, path=[],
        req=Req, params=[], session=undefined,
        handlers= [ {'query', wf:config('query', n2o_query)},
                    {session, wf:config(session, n2o_session)},
                    {route,   wf:config(route,   n2o_route)} ]}.
\end{lstlisting}
\vspace{1\baselineskip}

Chain of three N2O handlers is called
on each HTTP request. You can redefine any of them or plug your own
additional handler in chain to transform web server requests.

\vspace{1\baselineskip}
\begin{lstlisting}[caption={wf}\_{core}:fold/3]
    fold(Fun,Handlers,Ctx) ->
        lists:foldl(fun({_,Module},Ctx1) ->
            {ok,_,NewCtx} = Module:Fun([],Ctx1),
            NewCtx end,Ctx,Handlers).
\end{lstlisting}
\vspace{1\baselineskip}

\subsection{Query}
Query Handler parses URL query and HTTP form information from HTTP request.

\subsection{Session}
Session Handler manages key-value in-memory database ETS table that can be accessed.

\newpage
\subsection{Router}
You can specify routing table with application config:

\vspace{1\baselineskip}
\begin{lstlisting}
    {n2o, [{route,custom_router}]}
\end{lstlisting}
\vspace{1\baselineskip}

Remember that routing handler should be kept very simple because it
influences overal initial page load latency and HTTP capacity.

\vspace{1\baselineskip}
\begin{lstlisting}[caption=n2o\_route.erl]
    -module (n2o_route).
    -author('Maxim Sokhatsky').
    -include_lib("n2o/include/wf.hrl").
    -export(?ROUTING_API).

    finish(State, Ctx) -> {ok, State, Ctx}.
    init(State, Ctx) ->
        Path = wf:path(Ctx#context.req),
        Module = route_prefix(Path),
        {ok, State, Ctx#context{path=Path,module=Module}}.

    route_prefix(<<"/ws/",P/binary>>) -> route(P);
    route_prefix(<<"/",P/binary>>) -> route(P);
    route_prefix(P) -> route(P).

    route(<<>>)              -> index;
    route(<<"index">>)       -> index;
    route(<<"login">>)       -> login;
    route(<<"favicon.ico">>) -> static_file;
    route(_) -> index.
\end{lstlisting}
